# common script for stm32l5x and stm32u5x families

set _TARGETNAME $_CHIPNAME.cpu

if { [info exists CONNECT_UNDER_RESET] } {
	set _CONNECT_UNDER_RESET $CONNECT_UNDER_RESET
} else {
	set _CONNECT_UNDER_RESET 0
}

if { [info exists ENABLE_LOW_POWER] } {
	set _ENABLE_LOW_POWER $ENABLE_LOW_POWER
} else {
	set _ENABLE_LOW_POWER 0
}

if { [info exists STOP_WATCHDOG] } {
	set _STOP_WATCHDOG $STOP_WATCHDOG
} else {
	set _STOP_WATCHDOG 0
}

set _ENDIAN little

# Work-area is a space in RAM used for flash programming
# By default use 64kB at address 0x20000000
if { [info exists WORKAREASIZE] } {
	set $_TARGETNAME.workarea_size $WORKAREASIZE
} else {
	set $_TARGETNAME.workarea_size 0x10000
}

if { [info exists WORKAREAADDR] } {
	set _WORKAREAADDR $WORKAREAADDR
} else {
	set _WORKAREAADDR 0x20000000
}

# When RDP = 0x55, only a part of the RAM is set as non-secure by the secure
# application using the Secure Attribution Unit (SAU).

# This non-secure RAM cannot be auto-detected by the debugger, so to avoid
# programming issues, by default do not use the work-area and fall back to flash
# programming witouth flash loader (slower).
# If the user knows about the current SAU partitionning, he can provide
# the work-area address and size then force the usage of the loader by setting
# USE_LOADER_IN_RDP_05 to 1

# By default do not use the flash loader in RDP 0.5
if { [info exists USE_LOADER_IN_RDP_05] } {
	set _USE_LOADER_IN_RDP_05 $USE_LOADER_IN_RDP_05
} else {
	set _USE_LOADER_IN_RDP_05 0
}

#jtag scan chain
if { [info exists CPUTAPID] } {
	set _CPUTAPID $CPUTAPID
} else {
	if { [using_jtag] } {
		# STM32L5x: RM0438 Rev5, Section 52.2.8 JTAG debug port - Table 425. JTAG-DP data registers
		# STM32U5x: # RM0456 Rev1, Section 65.2.8 JTAG debug port - Table 661. JTAG-DP data registers
		# Corresponds to CortexÂ®-M33 JTAG debug port ID code
		set _CPUTAPID 0x0ba04477
	} {
		# SWD IDCODE (single drop, arm)
		set _CPUTAPID 0x0be12477
	}
}

swj_newdap $_CHIPNAME cpu -irlen 4 -ircapture 0x1 -irmask 0xf -expected-id $_CPUTAPID

dap create $_CHIPNAME.dap -chain-position $_TARGETNAME

target create $_CHIPNAME.ap0 mem_ap -dap $_CHIPNAME.dap -ap-num 0

target create $_TARGETNAME cortex_m -endian $_ENDIAN -dap $_CHIPNAME.dap -ap-num 0 -gdb-max-connections 2

# use non-secure RAM by default
$_TARGETNAME configure -work-area-phys $_WORKAREAADDR -work-area-size [set $_TARGETNAME.workarea_size] -work-area-backup 1

tpiu create $_CHIPNAME.tpiu -dap $_CHIPNAME.dap -ap-num 0 -baseaddr 0xE0040000

# create sec/ns flash and otp memories (sizes will be probed)
flash bank $_CHIPNAME.flash_ns      stm32l4x 0x08000000 0 0 0 $_TARGETNAME
flash bank $_CHIPNAME.flash_alias_s stm32l4x 0x0C000000 0 0 0 $_TARGETNAME
flash bank $_CHIPNAME.flash_otp     stm32l4x 0x0BFA0000 0 0 0 $_TARGETNAME

if { [info exists CLOCK_FREQ] } {
	set _CLOCK_FREQ $CLOCK_FREQ
} else {
	set _CLOCK_FREQ 4000
}
adapter speed $_CLOCK_FREQ

adapter srst delay 100
if { [using_jtag] } {
	jtag_ntrst_delay 100
}

if { ![using_hla] } {
	# if srst is not fitted use SYSRESETREQ to
	# perform a soft reset
	cortex_m reset_config sysresetreq
}

$_CHIPNAME.ap0 configure -event examine-end {
	# Enable Trace Port and Clock (uses more power)
	# DBGMCU_CR |= TRACE_EN
	mmw 0xE0044004 0x00000020 0
}

$_TARGETNAME configure -event examine-end {
global _ENABLE_LOW_POWER
global _STOP_WATCHDOG

	if { $_ENABLE_LOW_POWER == 1 } {
		# Enable debug during low power modes (uses more power)
		# DBGMCU_CR |= DBG_STANDBY | DBG_STOP
		mmw 0xE0044004 0x00000006 0
	} else {
		# Disable debug during low power modes
		# DBGMCU_CR |= ~(DBG_STANDBY | DBG_STOP)
		mmw 0xE0044004 0 0x00000006
	}
	if { $_STOP_WATCHDOG == 1 } {
		# Stop watchdog counters during halt
		# DBGMCU_APB1_FZ |= DBG_IWDG_STOP | DBG_WWDG_STOP
		mmw 0xE0044008 0x00001800 0
	} else {
		# Don't stop watchdog counters during halt
		# DBGMCU_APB1_FZ |= ~(DBG_IWDG_STOP | DBG_WWDG_STOP)
		mmw 0xE0044008 0 0x00001800
	}
}

proc ahb_ap_non_secure_access {} {
global _CHIPNAME

	# SPROT=1=Non Secure access, Priv=1
	$_CHIPNAME.dap apcsw 0x4B000000 0x4F000000
}

proc ahb_ap_secure_access {} {
global _CHIPNAME

	# SPROT=0=Secure access, Priv=1
	$_CHIPNAME.dap apcsw 0x0B000000 0x4F000000
}

proc is_secure {} {
	# read Debug Security Control and Status Regsiter (DSCSR) and check CDS (bit 16)
	set DSCSR [mrw 0xE000EE08]
	return [expr {($DSCSR & 0x10000) != 0}]
}

$_TARGETNAME configure -event halted {

	set secure [is_secure]

	if {$secure} {
		set secure_str "Secure"
		ahb_ap_secure_access
	} else {
		set secure_str "Non-Secure"
		ahb_ap_non_secure_access
	}

	# print the secure state only when it changes
	set _TARGETNAME [target current]
	global $_TARGETNAME.secure
	if {![info exists $_TARGETNAME.secure] || $secure != [set $_TARGETNAME.secure]} {
		echo "CPU in $secure_str state"
		# update saved security state
		set $_TARGETNAME.secure $secure
	}
}

$_TARGETNAME configure -event reset-init {
global _CLOCK_FREQ

	clock_config
	adapter speed $_CLOCK_FREQ
}

proc first_gdb_attach_hook {} {
global _TARGETNAME
global _CONNECT_UNDER_RESET
global _USE_LOADER_IN_RDP_05

	# Needed to be able to use the connect_assert_srst in reset_config
	if { $_CONNECT_UNDER_RESET == 1 } {
		reset init
	}

	set _TARGETNAME [target current]
	# check security status
	set secure [is_secure]

	# check flash options, from FLASH_OPTR register
	set optr [mrw 0x40022040]
	set tzen [expr {$optr & 0x80000000}]
	set rdp [expr {$optr & 0xFF}]

	if {$secure || $tzen} {
		ahb_ap_secure_access
	} else {
		ahb_ap_non_secure_access
	}

	# print the secure state only when it changes
	global $_TARGETNAME.secure
	set initialized [info exists $_TARGETNAME.secure]

	if {!$initialized || $secure != [set $_TARGETNAME.secure]} {
		# update saved security state
		set $_TARGETNAME.secure $secure

		echo [format "$_TARGETNAME in %s state" [expr {$secure ? "Secure" : "Non-Secure"}]]
	}

	# avoid some noise, when reset is asserted OPTR is read as zero
	if {$optr == 0} { return }

	# ensure that workarea is correctly configured if there is a change in tzen or rdp
	global $_TARGETNAME.tzen $_TARGETNAME.rdp
	set initialized [expr {[info exists $_TARGETNAME.tzen] && [info exists $_TARGETNAME.rdp]}]

	if {!$initialized || $tzen != [set $_TARGETNAME.tzen] || $rdp != [set $_TARGETNAME.rdp]} {
		# update saved tzen and rdp state
		set $_TARGETNAME.tzen $tzen
		set $_TARGETNAME.rdp $rdp

		echo [format "$_TARGETNAME TrustZone %s" [expr {$tzen ? "enabled" : "disabled"}]]

		# use secure workarea only when TZEN=1 and RDP!=0x55
		set workarea_addr [$_TARGETNAME cget -work-area-phys]
		if {$tzen && ($rdp != 0x55)} {
			set workarea_addr [expr {$workarea_addr | 0x10000000}]
		} else {
			set workarea_addr [expr {$workarea_addr & ~0x10000000}]
		}

		echo [format "$_TARGETNAME work-area address is set to 0x%08X" $workarea_addr]
		$_TARGETNAME configure -work-area-phys $workarea_addr

		# when RDP=0x55 (TZEN=1), only non-secure flash could be programmed
		# but workarea is not accessible since the RAM is secure.
		# to fall back to programming without loader set the workarea size to zero
		if {$rdp == 0x55 && !$_USE_LOADER_IN_RDP_05} {
			$_TARGETNAME configure -work-area-size 0
			echo "$_TARGETNAME work-area is disabled"
		} elseif {[$_TARGETNAME cget -work-area-size] == 0} {
			# restore the work-area size only if it was set previously to zero
			global $_TARGETNAME.workarea_size

			if {[set $_TARGETNAME.workarea_size] != 0} {
    			$_TARGETNAME configure -work-area-size [set $_TARGETNAME.workarea_size]
    			echo "$_TARGETNAME work-area is enabled"
			}
		}
	}
}

$_TARGETNAME configure -event gdb-attach { gdb_attach_hook }
$_TARGETNAME configure -event gdb-detach { gdb_detach_hook }
